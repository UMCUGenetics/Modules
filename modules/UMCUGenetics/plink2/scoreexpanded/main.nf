process PLINK2_SCOREEXPANDED {
	tag "${meta.id}"
	label 'process_low'

	conda "${moduleDir}/environment.yml"
	container "${workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container
		? 'https://depot.galaxyproject.org/singularity/plink2:2.00a2.3--h712d239_1'
		: 'biocontainers/plink2:2.00a2.3--h712d239_1'}"

	input:
	tuple val(meta), path(pgen), path(psam), path(pvar), path(afreq), path(scorefile)

	output:
	tuple val(meta), path("*.sscore"), emit: score
    tuple val("${task.process}"), val('plink2'), eval("plink2 --version 2>&1 | sed 's/^PLINK v//; s/ 64.*\$//'"), topic: versions, emit: versions_plink2

	when:
	task.ext.when == null || task.ext.when

	script:
	def args = task.ext.args ?: ''
	def prefix = task.ext.prefix ?: "${meta.id}"
	def mem_mb = task.memory.toMega()
	// plink is greedy
	"""
    plink2 \\
        --threads ${task.cpus} \\
        --memory ${mem_mb} \\
        --pfile ${pgen.baseName} \\
        --score ${scorefile} header cols=+scoresums,+denom,+fid no-mean-imputation \\
        --read-freq ${afreq} \\
        ${args} \\
        --out ${prefix}
    """


	stub:
	def prefix = task.ext.prefix ?: "${meta.id}"
	"""
	touch ${prefix}.sscore

	"""
}
